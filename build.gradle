plugins {
  id "com.vanniktech.maven.publish" version "0.32.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'signing'


group 'org.freeplane.lightdev'
version = '0.19.12-SNAPSHOT'

targetCompatibility='1.8'
sourceCompatibility='1.8'

repositories {
    mavenCentral()
	maven { url "${project.gradle.gradleUserHomeDir}/local-artifacts" }
}

dependencies {
	implementation 'javax.help:javahelp:2.0.05'
    implementation 'org.freeplane.dpolivaev.mnemonicsetter:mnemonicsetter:0.6'
    implementation 'commons-lang:commons-lang:2.6'
}

sourceSets {
    main {
        java {
            srcDir 'src'
            exclude 'com/lightdev/app/shtm/help/**'
        }
        resources {
            srcDir 'src'
            include 'com/lightdev/app/shtm/resources/**'
            exclude 'com/lightdev/app/shtm/help/**'
        }
    }
    
    help {
        java {
            srcDir 'src'
            include 'com/lightdev/app/shtm/help/**'
        }
        resources {
            srcDir 'src'
            include 'com/lightdev/app/shtm/help/**'
            exclude '**/*.java'
        }
    }
}

// Configure help source set to use main's dependencies
configurations {
    helpImplementation.extendsFrom implementation
    helpRuntimeOnly.extendsFrom runtimeOnly
}

// help.jar
task helpJar(type: Jar, dependsOn: helpClasses) {
    archiveBaseName = 'SimplyHTMLHelp'
    from sourceSets.help.output
}

// Configure the default jar task
jar {
    archiveBaseName = 'SimplyHTML'
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Main-Class': 'com.lightdev.app.shtm.App',
        )
    }
}

build.dependsOn helpJar

// Configure the sourcesJar task to avoid duplicates
afterEvaluate {
    tasks.named('sourcesJar') {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

import com.vanniktech.maven.publish.SonatypeHost
import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

mavenPublishing {
  configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
  publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

  signAllPublications()
  
    pom {
        name = project.name
        description = 'Application and java components for html'
        url = 'https://sourceforge.net/projects/simplyhtml/'

        scm {
            url = 'https://github.com/freeplane/shtml'
            connection = 'scm:git:https://github.com/freeplane/shtml.git'
            developerConnection = 'scm:git:git@github.com:freeplane/shtml.git'
        }

        licenses {
            license {
                name = 'GPL, Version 2 or later'
                url = 'https://www.gnu.org/licenses/gpl-3.0.html'
            }
        }

        developers {
            developer {
                id = 'lightdev'
                name = 'Ulrich Hilger'
            }
            developer {
                id = 'dpolivaev'
                name = 'Dimitry Polivaev'
                email = 'dpolivaev@gmx.de'
            }
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'localartifacts'
            url  = "${project.gradle.gradleUserHomeDir}/local-artifacts"
        }
    }
}

signing {
    sign publishing.publications.maven
}

javadoc {
    enabled = true
    options.encoding = "UTF-8"
    failOnError = false
}
