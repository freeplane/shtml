apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'org.freeplane.lightdev'
version = '0.18.7'

targetCompatibility='1.8'
sourceCompatibility='1.8'

repositories {
    mavenCentral()
	maven { url "${project.gradle.gradleUserHomeDir}/local-artifacts" }
}

dependencies {
	implementation 'javax.help:javahelp:2.0.05'
    implementation 'org.freeplane.dpolivaev.mnemonicsetter:mnemonicsetter:0.6'
    implementation 'commons-lang:commons-lang:2.6'
}

sourceSets {  
    main {  
        java {  
            srcDir 'src'  
        }  
    }  
    main {  
        resources {  
            srcDir 'src'
			include 'com/lightdev/app/shtm/resources/**'
			include 'com/lightdev/app/shtm/help/**'
        }  
    }  
} 

// help.jar
task helpJar(type: Jar) {
	baseName = 'SimplyHTMLHelp'
    from(sourceSets.main.output) {  
        include "com/lightdev/app/shtm/help/**" 
    }  
}

// lib.jar
task mainJar(type: Jar, dependsOn: classes) {  
	baseName = 'SimplyHTML'
    from(sourceSets.main.output) {  
        exclude "com/lightdev/app/shtm/help/**" 
    }
	manifest {
	def classPath = (configurations.compile.collect { it.name } +
                   helpJar.outputs.files.collect { it.name }).join(' ')
    attributes(
		'Implementation-Title': project.name,
        'Implementation-Version': project.version,
        'Main-Class': 'com.lightdev.app.shtm.App',
        'Class-Path': classPath
		)
  }	
}

task sourceJar(type: Jar) {
    baseName = 'SimplyHTML'
    classifier = 'sources'
    from (sourceSets.main.allSource){  
        exclude "com/lightdev/app/shtm/help/**" 
    }
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
build.dependsOn mainJar
build.dependsOn javadoc

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'simplyhtml'
			groupId = project.group
			version = project.version
			artifact mainJar
			artifact sourceJar
			artifact javadocJar
            pom {
                name = project.name
                description = 'Application and java components for html'
                url = 'https://sourceforge.net/projects/simplyhtml/'

                scm {
                    url = 'https://github.com/freeplane/shtml'
                    connection = 'scm:git:https://github.com/freeplane/shtml.git'
                    developerConnection = 'scm:git:git@github.com:freeplane/shtml.git'
                }

                licenses {
                    license {
                        name = 'GPL, Version 2 or later'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }

                developers {
                    developer {
                        id = 'lightdev'
                        name = 'Ulrich Hilger'
                    }
                    developer {
                        id = 'dpolivaev'
                        name = 'Dimitry Polivaev'
                        email = 'dpolivaev@gmx.de'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
        maven { 
			 url  = "${project.gradle.gradleUserHomeDir}/local-artifacts"
		}
    }
}

signing {
    sign publishing.publications.maven
}

javadoc {
    enabled = true
    options.encoding = "UTF-8"
    failOnError = false
}

apply from: './dist.gradle'
