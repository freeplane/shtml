plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

group 'com.lightdev.app.shtm.simplyhtml'
version = '0.18.3'

targetCompatibility='1.8'
sourceCompatibility='1.8'

repositories {
    jcenter()
	maven { url "${project.gradle.gradleUserHomeDir}/local-artifacts" }
	maven { url 'https://dl.bintray.com/freeplane/freeplane' }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
	publications = ['simplyhtml'] 
	pkg {
        repo = 'freeplane'
        name = 'simplyhtml'
        userOrg = 'freeplane'
        licenses = ['GPL-3.0', 'GPL-2.0']
        vcsUrl = 'https://github.com/dpolivaev/simplyhtml.git'
		version {
                name = project.version
		}
    }
}

dependencies {
	compile 'javax.help:javahelp:2.0.05',
          'org.dpolivaev.mnemonicsetter:mnemonicsetter:0.5',
          'commons-lang:commons-lang:2.6'
}

sourceSets {  
    main {  
        java {  
            srcDir 'src'  
        }  
    }  
    main {  
        resources {  
            srcDir 'src'
			include 'com/lightdev/app/shtm/resources/**'
			include 'com/lightdev/app/shtm/help/**'
        }  
    }  
} 

// help.jar
task helpJar(type: Jar) {
	baseName = 'SimplyHTMLHelp'
    from(sourceSets.main.output) {  
        include "com/lightdev/app/shtm/help/**" 
    }  
}

// lib.jar
task mainJar(type: Jar, dependsOn: classes) {  
	baseName = 'SimplyHTML'
    from(sourceSets.main.output) {  
        exclude "com/lightdev/app/shtm/help/**" 
    }
	manifest {
	def classPath = (configurations.compile.collect { it.name } +
                   helpJar.outputs.files.collect { it.name }).join(' ')
    attributes(
		'Implementation-Title': project.name,
        'Implementation-Version': project.version,
        'Main-Class': 'com.lightdev.app.shtm.App',
        'Class-Path': classPath
		)
  }	
}

task sourceJar(type: Jar) {
    baseName = 'SimplyHTML'
    classifier = 'sources'
    from (sourceSets.main.allSource){  
        exclude "com/lightdev/app/shtm/help/**" 
    }
}

build.dependsOn mainJar
build.dependsOn javadoc

publishing {
    publications {
        simplyhtml(MavenPublication) {
            artifactId 'SimplyHTML'
			artifact mainJar
			artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
		maven { url "${project.gradle.gradleUserHomeDir}/local-artifacts"}
    }
}

javadoc {
    enabled = true
    options.encoding = "UTF-8"
    failOnError = false
}

apply from: './dist.gradle'
