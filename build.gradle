plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

group 'com.lightdev.app.shtm.simplyhtml'
version = '0.16.15'

targetCompatibility='1.7'
sourceCompatibility='1.7'

repositories {
    jcenter()
	maven { url "http://dl.bintray.com/freeplane/freeplane" } 
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
	publications = ['simplyhtml'] 
	pkg {
        repo = 'freeplane'
        name = 'simplyhtml'
        userOrg = 'freeplane'
        licenses = ['GPL-3.0', 'GPL-2.0']
        vcsUrl = 'https://github.com/dpolivaev/simplyhtml.git'
		version {
                name = project.version
		}
    }
}

dependencies {
	compile 'javax.help:javahelp:2.0.05',
			'gnu-regexp:gnu-regexp:1.1.4',
			'org.dpolivaev.mnemonicsetter:mnemonicsetter:0.3'  
}

sourceSets {  
    main {  
        java {  
            srcDir 'src'  
        }  
    }  
    main {  
        resources {  
            srcDir 'src'
			include 'com/lightdev/app/shtm/resources/**'
			include 'com/lightdev/app/shtm/help/**'
        }  
    }  
} 

// holds classes included into SimplyHTMLHelp.jar so they are to be excluded from SimplyHTML.jar
ext.helpExcludes = []

// help.jar
task helpJar(type: Jar) {
	baseName = 'SimplyHTMLHelp'
    from(sourceSets.main.output) {  
        include "com/lightdev/app/shtm/help/**" 
        project.ext.helpExcludes.addAll(includes)
    }  
}

// lib.jar
task mainJar(type: Jar, dependsOn: classes) {  
	baseName = 'SimplyHTML'
    from(sourceSets.main.output) {  
        exclude project.ext.helpExcludes
    }
	manifest {
	def classPath = (configurations.compile.collect { it.name } +
                   helpJar.outputs.files.collect { it.name }).join(' ')
    attributes(
		'Implementation-Title': project.name,
        'Implementation-Version': project.version,
        'Main-Class': 'com.lightdev.app.shtm.App',
        'Class-Path': classPath
		)
  }	
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        simplyhtml(MavenPublication) {
            from components.java
            artifactId 'SimplyHTML'
			artifacts = [mainJar]
			artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

apply from: './dist.gradle'