task binDistrib(type: Zip) {
    archiveName = 'simplyhtml_bin-' + project.version + '.zip'
    destinationDir = rootDir

    from(configurations.compile)
    from(mainJar.outputs.files)
    from(helpJar.outputs.files)
    from(rootDir) {
        include('*.txt')
    }

    into('simplyhtml-' + project.version)

    dependsOn mainJar
    dependsOn helpJar
}

task sourceDistrib(type: Tar) {
    archiveName = 'simplyhtml_src-' + project.version + '.tar.gz'
    destinationDir = rootDir
    compression = 'GZIP'
    includeEmptyDirs = false

    from(rootDir) {
        include('src/**')
        include('*.txt')
        include('*.gradle')
    }

    into('simplyhtml-' + project.version)
}

task dist {
    dependsOn binDistrib
    dependsOn sourceDistrib
}

task cleanDist(type: Delete) {
    delete binDistrib.outputs.files
    delete sourceDistrib.outputs.files
}
clean.dependsOn cleanDist
